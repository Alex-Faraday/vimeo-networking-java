buildscript {
    ext.kotlin_version = '1.4.32'
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.16.0"
    id "com.github.ben-manes.versions" version "0.38.0"
}

ext {
    retrofitVersion = '2.9.0'
    okioVersion =  '2.10.0'
    moshiVersion = '1.12.0'
    stagVersion = '2.6.0'
    junitVersion = '4.13.2'
    buildToolsVersion = '29.0.3'
    classgraphVersion = '4.8.104'
}

subprojects {
    if (path.contains("integrations") || path.contains("models-") || path.contains("example")) {
        return 
    }

    apply {
        plugin "io.gitlab.arturbosch.detekt"
    }
    apply from: "../detekt_configuration.gradle"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    apply plugin: "com.jfrog.bintray"

    tasks.withType(Javadoc).all { enabled = false }

    tasks.withType(JavaCompile) { options.fork = true }
}

def isNonStable = { String version ->
    def unStableKeyword = ['ALPHA', 'RC', 'BETA', 'DEV', "M"].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return unStableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

tasks.named('wrapper') {
    distributionType = Wrapper.DistributionType.ALL
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
